Import('env')
Import("env_modules")

import os

# Need to add the include path to env so the vsproj generator consumes it.
if env["vsproj"]:
    env.Append(CPPPATH=["spine-cpp/include"])

# Module name
module = os.path.splitext(os.path.basename(os.path.realpath(".")))[0]

env_spine_runtime = env_modules.Clone()
env_spine_runtime.Append(CPPPATH=["spine-cpp/include"])

sources = Glob("spine-cpp/src/spine/*.cpp") + Glob("*.cpp")

# Needed on Clang to not have a gazillion -Winconsistent-missing-override warnings for GDCLASS
# I do not understand why other modules using GDCLASS do not have this issue when compiling.
if not env_spine_runtime.msvc:
    env_spine_runtime.Append(CXXFLAGS=["-Wno-inconsistent-missing-override"])

# Compile as a static library
lib = env_spine_runtime.Library(module, sources)
# Add the library as a dependency of the final executable
env.Prepend(LIBS=[lib])
